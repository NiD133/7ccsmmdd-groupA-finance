grammar mdd.groupA.finance.Finance with org.eclipse.xtext.common.Terminals

generate finance "http://www.groupA.mdd/finance/Finance"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

FinanceProgram:
 statements += Statements*
;

Statements:
	 IncomeStatement |
	 SpendStatement |
	 InvestStatement |
	 VarDeclaration |
	 CurrDeclaration |
	 TagDeclaration
;

InvestStatement:
	 "Invest:" value=REAL currency=Currency
	 "Date:" date=DATE
	 "Tag:" tag=Tag
	 ("Description:" description=STRING)?
;

SpendStatement:
	 "Spend:" value=REAL currency=Currency
	 "Date:" date=DATE
	 "Tag:" tag=Tag
	 ("Description:" description=STRING)?
;

IncomeStatement:
	 "Income:" value=REAL currency=Currency
	 "Date:" date=DATE
	 "Tag:" tag=Tag
	 ("Description:" description=STRING)?
;

Tag:
 	commonTag=CommonTag | tagVar=TagVar
;

enum CommonTag:
	 EDU = "Education" |
	 LEI = "Leisure" |
	 DAI = "Daily" |
	 BEA = "Beauty" |
	 GRO = "Grocery" |
	 TRA = "Transfer"
;

TagVar:
 	tagRef=[TagDeclaration]
;

TagDeclaration:
 	"tag" name=ID "=" value=STRING
;

Currency:
 	commonCurrency=CommonCurrency | currencyVar=CurrencyVar
;

enum CommonCurrency:
	 GBP = "GBP" |
	 USD = "USD" |
	 CNY = "CNY" |
	 EUR = "EUR" 
;

CurrDeclaration:
 	"currency" name=ID "=" value=STRING
;

CurrencyVar:
 	currencyRef=[CurrDeclaration]
;

DATE returns ecore::EDate:
    '\'' INT '-' INT '-' INT '\'';

VarDeclaration:
 	"var" name=ID "=" value=INT
;

REAL returns ecore::EFloat hidden():
 	INT? "." INT
;
